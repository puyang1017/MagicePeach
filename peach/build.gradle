plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
}

android {
    compileSdk 29

    defaultConfig {
        minSdk 21
        targetSdk 29

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    //自动追加版本号和版本名称
    android.libraryVariants.all { variant ->
        if(variant.name.equalsIgnoreCase("release")) {
            // 修改aar名称
            variant.outputs.all { output ->
                def f = output.outputFileName
                if (f != null && f.endsWith('.aar')) {
                    output.outputFileName = "PeachSdk.aar"
                }
            }
            // 复制aar到指定目录
            variant.assemble.doLast {
                variant.outputs.all { output ->
                    // 输出目录在：demo项目目录/libs/
                    def outputToDemoPath = [project.rootDir.absolutePath, "app", "libs"].join(File.separator)
                    println("==================>outputToDemoPath:${outputToDemoPath}")
                    copy {
                        from output.outputFile
                        into outputToDemoPath
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.work:work-runtime:2.7.1'
}